/*
 * Copyright 2017 Evgeny Timofeev
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        signing {
            keyAlias keystoreProperties['KeyAlias']
            keyPassword keystoreProperties['KeyPassword']
            storeFile file(keystoreProperties['KeyLocation'])
            storePassword keystoreProperties['KeyPassword']
        }
    }
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.eblis.whenwasit"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 29
        versionName "1.0.${versionCode}"
        resConfigs "auto"
        vectorDrawables.useSupportLibrary = true
        resValue "string", "version_name", versionName
        manifestPlaceholders = [fabric_api_key: keystoreProperties['FabricApiKey']]
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        debug {
            minifyEnabled false
            resValue "string", "banner_ad_id", keystoreProperties['BannerAdIdDebug']
            buildConfigField "String", "ADMOB_ID", keystoreProperties['AdMobId']
            buildConfigField "String", "REWARDS_AD_ID", keystoreProperties['RewardsAdIdDebug']
            buildConfigField "String", "INTERSTITIAL_AD_ID", keystoreProperties['InterstitialAdIdDebug']
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "banner_ad_id", keystoreProperties['BannerAdIdRelease']
            buildConfigField "String", "ADMOB_ID", keystoreProperties['AdMobId']
            buildConfigField "String", "REWARDS_AD_ID", keystoreProperties['RewardsAdIdRelease']
            buildConfigField "String", "INTERSTITIAL_AD_ID", keystoreProperties['InterstitialAdIdRelease']
            signingConfig signingConfigs.signing
        }
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def androidSupportVersion = '27.1.1'
def firebaseVersion = '12.0.1'
def butterKnifeVersion = '8.8.1'

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // Android support library
    compile "com.android.support:appcompat-v7:${androidSupportVersion}"
    compile "com.android.support:design:${androidSupportVersion}"
    compile "com.android.support:recyclerview-v7:${androidSupportVersion}"
    compile "com.android.support:cardview-v7:${androidSupportVersion}"
    compile "com.android.support:support-v13:${androidSupportVersion}"
    compile "com.android.support:support-vector-drawable:${androidSupportVersion}"
    // Firebase
    compile "com.google.firebase:firebase-core:${firebaseVersion}"
    compile "com.google.firebase:firebase-ads:${firebaseVersion}"
    compile "com.google.firebase:firebase-perf:${firebaseVersion}"
    //ads
    compile "com.google.android.gms:play-services-ads:${firebaseVersion}"
    // ButterKnife
    compile "com.jakewharton:butterknife:${butterKnifeVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterKnifeVersion}"
    // Material date and time picker
    compile 'com.wdullaer:materialdatetimepicker:3.4.1'
    // Rate this app
    compile 'io.github.kobakei:ratethisapp:1.2.0'
    //implementation 'net.danlew:android.joda:2.9.9.3'
    compile 'joda-time:joda-time:2.9.9'
    // Crashlytics
    compile('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true
    }
    // JUnit
    testCompile 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'