/*
 * Copyright 2017 Evgeny Timofeev
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.0'

    defaultConfig {
        applicationId "com.djonique.birdays"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 14
        versionName "1.3.3"
        resConfigs "auto"
        vectorDrawables.useSupportLibrary = true
        resValue "string", "version_name", versionName
        manifestPlaceholders = [fabric_api_key:keystoreProperties['FabricApiKey']]
    }

    buildTypes {
        debug {
            resValue "string", "banner_ad_id", keystoreProperties['BannerAdIdDebug']
            buildConfigField "String", "INTERSTITIAL_AD_ID", keystoreProperties['InterstitialAdIdDebug']
            minifyEnabled false
        }
        release {
            resValue "string", "banner_ad_id", keystoreProperties['BannerAdIdRelease']
            buildConfigField "String", "INTERSTITIAL_AD_ID", keystoreProperties['InterstitialAdIdRelease']
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def androidSupportVersion = '27.0.0'
def firebaseVersion = '11.4.2'
def butterKnifeVersion = '8.8.1'

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    // Android support library
    compile "com.android.support:appcompat-v7:${androidSupportVersion}"
    compile "com.android.support:design:${androidSupportVersion}"
    compile "com.android.support:recyclerview-v7:${androidSupportVersion}"
    compile "com.android.support:cardview-v7:${androidSupportVersion}"
    compile "com.android.support:support-v4:${androidSupportVersion}"
    compile "com.android.support:support-vector-drawable:${androidSupportVersion}"

    // Firebase
    compile "com.google.firebase:firebase-core:${firebaseVersion}"
    compile "com.google.firebase:firebase-messaging:${firebaseVersion}"
    compile "com.google.firebase:firebase-ads:${firebaseVersion}"
    compile "com.google.firebase:firebase-perf:${firebaseVersion}"

    // ButterKnife
    compile "com.jakewharton:butterknife:${butterKnifeVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterKnifeVersion}"

    // Material date and time picker
    compile 'com.wdullaer:materialdatetimepicker:2.5.0'

    // Rate this app
    compile 'io.github.kobakei:ratethisapp:1.2.0'

    // Crashlytics
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }

    // JUnit
    testCompile 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'